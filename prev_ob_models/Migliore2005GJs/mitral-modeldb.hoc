begintemplate Mitral

public soma, priden, secden, tuftden, hillock, initialseg
public dendritic, somatic, second, somden

create soma, priden, secden[2], tuftden, hillock, initialseg

objref dendritic, somatic, somden

proc init() {
forall {Ra = 150}
dendritic = new SectionList()
forsec "priden" dendritic.append()
forsec "secden" dendritic.append()
forsec "tuftden" dendritic.append()

somatic = new SectionList()
soma somatic.append
hillock somatic.append

somden = new SectionList()
forsec somatic somden.append()
forsec dendritic somden.append()

topol()
segments()
geometry()
memb()

}


proc topol() {local i
	connect secden[0](0), soma(.5)
	connect secden[1](0), soma(.5)
	connect priden(0), soma(1)
	connect tuftden(0), priden(1)

	connect hillock(0), soma(0)
	connect initialseg(0), hillock(1)
}

proc segments() {local i
	soma.nseg= 1
	priden.nseg = 5
	forsec "tuftden" nseg = 30
	forsec "secden" nseg = 4
	initialseg.nseg = 3
	hillock.nseg = 3
}

proc geometry() {local i
	soma { L = 25  diam = 20 }
	priden { L = 300  diam = 3 }
	forsec "tuftden" {rallbranch=20 L=300  diam(0:1)=.4:.4 }
	forsec "secden" { L=100  diam=2 }
	initialseg{ L=30  diam=1.5}
	hillock { L=5  diam(0:1) = soma.diam(0) : initialseg.diam(0) }
	define_shape()
}
	


proc memb() {
	forall {insert pas}
	forsec somden {
		insert nax  insert kamt insert kdrmt
		ek = -90
		ena = 50
		gbar_nax = 0.04 sh_nax=10
	 	gbar_kdrmt = 0.0001
		gbar_kamt = 0.004
		}

	forall {
		cm = 1.8
		g_pas = 1/12000
		e_pas = -65
	}

	initialseg {
		insert nax  insert kamt insert kdrmt
		ek = -90
		ena = 50
	        g_pas = 1/1000
	        gbar_nax = 0.4
		sh_nax = 0
	        gbar_kamt = 0.04
	        gbar_kdrmt = 0.0001
	}       
}

endtemplate Mitral
